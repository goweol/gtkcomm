SCRIPT language
===============

NOTE
----

    "string": 문자열. '\n'이나 '\008'과 같은 control character를 포함할 수
	     있다.
    #       : '#' 이후는 comment로 간주된다. string이 #을 포함할 경우에는
	     반드시 string을 ' 나 " 로 묶어주어야 한다.


COMMAND LIST
------------

-.  ASSIGN
    사용: assign varname "string"
    설명: 새로운 변수를 생성한다. 이후 다른 데서
         이 변수를 '$'를 붙여서 참고할 수 있다.
    예제: assign prompt "\$"
	  assign filename "~/somewhere/upload/filename.ext"

	  transmit $filename	# filename send
	  waitfor $prompt	# wait prompt

-.  AUTORES
    사용: autores [add/gadd] "wait-string" [string/script/sfile]
			     "string-script-file"
    설명: auto response 목록에 추가. remote에서 wait-string을 받으면
	  그 뒤 parameter가 string이면 string을 transmit하고, script이면
	  script를 실행한다. 'add' 사용시에는 disconnect시에 remove된다.
	  gadd한 경우에는 gtkcomm을 종료할 때까지 유지된다.
    예제: autores gadd "[ENTER]를 입력하세요" string "\r"
	  autores add "Send file with zmodem" script 'sendfile zmodem "?"'
	  autores add "Send file with zmodem" sfile 'sendfile.scr'

-.  BEEP
    사용: beep <percent>
    설명: 삑 소리를 낸다. 'percent'가 없는 경우 config의 BeepPercent 값이
	  사용된다. 'percent'의 범위는 -100 ~ 100.
    예제: beep
	  beep 50
	  beep -50

-.  BOOKMARK
    사용: bookmark [add/gadd] "bookmark-name" [string/script/sfile]
			      "string-script-file"
    설명: Bookmark menu에 새로운 menu item을 추가한다.
	  gadd는 global add의 약자로서 disconnect되어도 삭제되지 않는다.
	  add를 사용한 경우 disconnect될 때 bookmark entry에서 삭제된다.
    예제: bookmark add "바로가기/리눅스동호회" string "go linux\r"
	  bookmark add "바로가기/리눅스동호회" script "transmit 'go linux\n'"
	  bookmark add "리눅스동호회" sfile "golinux.scr"
 
-.  BUTTON
    사용: button [add/gadd] "button-name" [string/script/sfile]
			   "string-script-file"
    설명: control-bar에 새로운 button을 추가한다.
	  gadd는 global add의 약자로서 disconnect되어도 삭제되지 않는다.
	  add를 사용한 경우 disconnect될 때 button이 삭제된다.
    예제: button add "TOP" string "t\r"
	  button gadd "FAST" script 'transmit "chgbaud 115200\r"; \
	                             waitfor "Change baud to 115200"; \
				     set baudrate 115200; \
				     pause 3; transmit "\r"'
	  button add "SLOW" sfile 'slow.scr'
 
-.  CAPTURE
    사용: capture [on/off] <filename> <excludebuf>
    설명: capture file을 열거나 닫는다. 'capture on' 시에는 반드시 filename이
	  주어져야 하며, 'excludebuf' keyword가 없는 경우 현재 terminal
	  buffer의 내용을 capture file에 포함한다.
    예제: capture on "today.log" excludebuf
	  capture on "today.log"
	  capture off
 
-.  CLEAR
    사용: clear
    설명: 화면을 지운다.
    예제: clear
 
-.  CTLMENU
    사용: ctlmenu [add/gadd] title itemname [string/script/sfile]
				"string-script-file"
    설명: control-bar에 사용자 메뉴를 추가한다. add 사용시에는 disconnect시
	  자동으로 삭제된다.
    예제: ctlmenu gadd Baudrate 9600 script 'set baudrate 9600'
	  ctlmenu gadd Baudrate 4800 script 'set baudrate 4800'
	  ctlmenu gadd Baudrate 2400 script 'set baudrate 2400'
	  ctlmenu add Download FAST sfile 'fastdown.scr'
	  ctlmenu add Download SLOW sfile 'slowdown.scr'

-.  DISCONNECT
    사용: disconnect
    설명: 연결을 끊는다.
    예제: disconnect
 
-.  DOWNLOAD
    사용: receivefile zmodem
    설명: file을 download한다.
    예제: receivefile zmodem
 
-.  EMULATE
    사용: emulate [vt100 | ansi]
    설명: terminal emulation mode를 설정.
    예제: emulate vt100
	  emulate ansi
 
-.  END
    사용: end
    설명: script finish. goto와 연관되어 사용될 수 있다.
    예제: goto next
	  here:
	    message "works are done"
	    end
	  next:
	    transmit "test goto and end"
	    goto here

-.  ENDWIN
    사용: endwin
    설명: 현재 열린 dialog 창이 있으면 그 창을 닫음.

-.  GET BAUDRATE
    사용: get baudrate currbaud
    설명: 현재 baudrate를 'currbaud' 변수로 얻어온다.
    예제: get baudrate currbaud
	  if $currbaud != '115200'
	    set baudrate 115200
	  fi

-.  GET FILE
    사용: get file filename size filesize
    설명: 'filename'의 size를 'filesize' 변수로 얻어온다.
    예제: assign filename "~/upload/file.tar.gz"
	  get file $filename size filesize
	  message "The size of the $filename is $filesize"

-.  GOTO
    사용: goto label
    설명: label로 점프
    예제: goto next
	    transmit "skip transmit\r"
	  next:
	    transmit "do transmit\r"

-.  IF/ELSE/ELSEIF/ELIF/FI/ENDIF
    사용: if $? [==/!=] #num
    설명: if 조건문. 이전 명령의 실행결과를 확인하는 기능으로서
	  현재는 주로 waitfor와 물려서 사용될 수 있다. 모든 Script
	  명령의 실행 결과는 성공이면 0, 실패면 -1 값을 갖는다.

	  elseif와 elif는 동일하며, 마찬가지로 fi와 endif는 동일하다.

    예제: waitfor "login:" 2
	  if $? == 0
	    transmit myID
	    waitfor "Password:" 2
	    if $? != -1
	       transmit myPASSWORD
	    fi
	  else
	    message "\n'login:' wait timeout. Login manually!\n"
	  fi

    예제: if "$variable" == "value"
	    message "value\n"
	  elseif "$variable" == "other_val"
	    message "other_val\n"
	  endif

-.  INCLUDE
    사용: include "script-filename"
    설명: 다른 script file을 include
    예제: include "my-local.scr"
 
-.  KFLUSH
    사용: kflush 
    설명: key buffer flush?  현재는 아무 동작도 수행하지 않는다.
    예제: kflush
 
-.  LABEL
    사용: label [add/gadd] [baudrate/emulate/protocol/linestatus]
    설명: control-bar에 baudrate, emulate, protocol의 상태 label을 추가한다.
	  'linestatus'의 경우 control-bar에 line status를 보여주는 toggle
	  button 같은 것을 추가.
    예제: label add baudrate

-.  MESSAGE
    사용: message "string"
    설명: 화면에 string을 프린트. transmit처럼 remote로 send하지는 않는다.
    예제: message "Hello, world!!!"
 
-.  RUN
    사용: run 'filename'
    설명: 'filename' file을 실행.
    예제: run 'download.sh'
 
-.  SET BAUDRATE
    사용: set baudrate [300/600/1200/2400/4800/9600/19200/38400/57600/115200]
    설명: modem의 baudrate를 바꾼다. 일반적으로 COM port direct connection
	  환경에서 유용하게 사용된다.
    예제: set baudrate 19200
 
-.  SET CONFIG
    사용: set config [variable-name] [value]
    설명: gtkcommrc file의 모든 variable들의 값을 바꿀 수 있다.
	  (주의: TermFont, DoLock과 같은 변수는 이미 초기화가 끝난
		 경우에는 동작에 영향을 받지 않는다. 그런 변수들이 여럿 있다)
    예제: set config LeftMouseClickStr	  "p\r"
	  set config UseBeep		  TRUE
	  set config ColorCyan	          "0.0 0.6 0.6"
	  set config IdleGuardInverval	  240
 
-.  SET TITLE
    사용: set title "title-string"
    설명: 창의 제목을 바꾼다.
    예제: set title "하이텔"
 
-.  STSMSG
    사용: stsmsg "string"
    설명: status bar에 string을 프린트.
    예제: stsmsg "Hello, world!!!"
 
-.  PAUSE
    사용: pause [secs]
    설명: 'secs' 초 동안 sleep. 'secs'는 실수(float) 단위이고, 0.1초 이상.
    예제: pause 3
	  pause 0.5
 
-.  RFLUSH
    사용: rflush
    설명: rx buffer flush? 현재는 아무 동작도 수행하지 않는다.
    예제: rflush
 
-.  TRANSMIT
    사용: transmit "string"
    설명: 문자열을 remote로 보낸다.
    예제: transmit "go linux\r"
 
-.  UPLOAD
    사용: sendfile [ascii/raw-ascii/xmodem/ymodem/zmodem] filename
    설명: file을 주어진 프로토콜을 이용하여 송신한다.
    예제: sendfile xmodem "test.lod"
	  sendfile zmodem "?"	       # "?"면 file browse 창이 열린다.
 
-.  WAITFOR
    사용: waitfor "string" <secs> <"string2">
    설명: 문자열을 기다린다. 'secs'가 주어진 경우에는 주어진 시간(초) 동안만
	  기다린다. 'secs'가 없거나 0인 경우에는 무한히 기다린다.
	  'secs'가 있는 경우 두번째 "string2"를 추가할 수 있고, 이 경우
	  "string"과 "string2" 둘중 하나를 만나면 waitfor는 종료하며,
	  "string"을 만나면 result는 0, "string2"를 만나면 result는 1이다.
    예제: waitfor "$"
	  waitfor "XYZ" 3

    예제2:
	  waitfor "ABC" 5.5 "DEF"
	  if $? == 1
	    message "met DEF"
	  fi

-.  WIN
    사용: win begin "title"
	  win ...
    설명: 대화상자 창을 생성하고, button을 만들고...
    예제: 자세한 사항은 res/script/dialog.scr 참고.

